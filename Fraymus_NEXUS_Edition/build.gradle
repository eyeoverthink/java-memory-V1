plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

version '1.0.0-ULTRA'
group = 'com.eyeoverthink'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

def osName = System.getProperty('os.name').toLowerCase()
def lwjglNativesValue = osName.contains('win') ? 'natives-windows' :
        (osName.contains('mac') ? 'natives-macos' : 'natives-linux')
def imguiNativesSuffix = osName.contains('win') ? 'windows' :
        (osName.contains('mac') ? 'macos' : 'linux')

repositories {
    mavenCentral()
}

project.ext.lwjglVersion = "3.3.3"
project.ext.jomlVersion = "1.10.5"
project.ext.lwjglNatives = lwjglNativesValue
project.ext.imguiVersion = "1.86.11"

dependencies {
    implementation "io.github.spair:imgui-java-binding:$imguiVersion"
    implementation "io.github.spair:imgui-java-lwjgl3:$imguiVersion"
    runtimeOnly "io.github.spair:imgui-java-natives-${imguiNativesSuffix}:$imguiVersion"

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"

    implementation "org.joml:joml:$jomlVersion"

    implementation "org.apache.pdfbox:pdfbox:2.0.31"
    
    // MongoDB driver for cloud persistence (The Vault)
    implementation "org.mongodb:mongodb-driver-sync:4.11.0"
    
    // QR Code generation (Visual backup)
    implementation "com.google.zxing:core:3.5.2"
    implementation "com.google.zxing:javase:3.5.2"
    
    // JSON serialization
    implementation "com.google.code.gson:gson:2.10.1"
    
    // LibGDX for 3D visualization
    implementation "com.badlogicgames.gdx:gdx:1.12.1"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:1.12.1"
    implementation "com.badlogicgames.gdx:gdx-platform:1.12.1:natives-desktop"
}

application {
    mainClass = 'fraymus.Main'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes 'Main-Class': 'fraymus.Main'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveBaseName = 'Fraymus_NEXUS_ULTIMATE'
    archiveVersion = '1.0-GENESIS'
}

run {
    jvmArgs = ['-Djava.awt.headless=false']
}

shadowJar {
    archiveBaseName.set('Fraymus_God_Mode')
    archiveClassifier.set('')
    archiveVersion.set('')
    manifest {
        attributes 'Main-Class': 'fraymus.Main'
    }
}
